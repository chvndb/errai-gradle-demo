apply plugin: "war"
apply plugin: "eclipse"

ext {
  gwtDevModules = ["demo.errai.App"]
  gwtModules = gwtDevModules
  gwtLogLevel = allowCliOverride("gwtLogLevel", "INFO")
  gwtLocalWorkers = "2"
  gwtBuildDir = "$buildDir/gwt"
  gwtExtraDir = "$buildDir/gwt-extra"
  deployDir = "$projectDir/war"
}

def allowCliOverride(prop, defaultVal) {
  hasProperty(prop) ? getProperty(prop) : defaultVal
}

def exec(cmd) {
  cmd.execute().text.trim()
}

repositories {
  mavenCentral()
}

dependencies {
  def gwt = "2.8.0"
  def errai = "4.0.0.CR1"
  def gmd = "2.0-rc3"
  def gmdJquery = "1.0-rc3"
  def gmdTable = "1.0-rc3"

  providedCompile "com.google.gwt:gwt-user:$gwt"
  providedCompile "com.google.gwt:gwt-dev:$gwt"
  runtime "com.google.gwt:gwt-servlet:$gwt"

  compile "org.jboss.errai:errai-javaee-all:$errai"
  compile "org.jboss.errai:errai-jboss-as-support:$errai"

  providedCompile "com.github.gwtmaterialdesign:gwt-material:$gmd"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-addins:$gmd"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-themes:$gmd"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-jquery:$gmdJquery"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-table:$gmdTable"
}

task gwtCompile(type:JavaExec) {
  inputs.file(sourceSets.main.java.srcDirs).skipWhenEmpty()
  inputs.dir sourceSets.main.output.resourcesDir

  outputs.dir buildDir
  outputs.upToDateSpec = new org.gradle.api.specs.AndSpec()

  doFirst { file(buildDir).mkdirs() }

  main = "com.google.gwt.dev.Compiler"

  classpath {
    [
      sourceSets.main.java.srcDirs,           // Java source
      sourceSets.main.output.resourcesDir,    // Generated resources
      sourceSets.main.output.classesDir,      // Generated classes
      sourceSets.main.compileClasspath,       // Deps
    ]
  }

  args =
    [
      gwtModules,
      "-war", gwtBuildDir,
      "-logLevel", gwtLogLevel,
      "-localWorkers", gwtLocalWorkers,
      "-compileReport",
      "-generateJsInteropExports",
      "-extra", gwtExtraDir
    ].flatten()
}
war.dependsOn gwtCompile
war {
  from gwtBuildDir
}

clean.doFirst {
  delete "${projectDir}/.errai"
  delete "${projectDir}/war"
}

task deploy(type: Copy) {
  doFirst { file(deployDir).mkdirs() }
  doFirst { delete "$deployDir/app.war" }
  into "$deployDir/app.war"
  with war
}
deploy.dependsOn war

task stop {
  doLast { exec("docker-compose -f src/main/docker/wildfly.yml down") }
}

task start {
  doFirst { file(deployDir).mkdirs() }
  doLast { exec("docker-compose -f src/main/docker/wildfly.yml up -d") }
}
start.dependsOn stop
start.dependsOn clean
start.finalizedBy deploy
