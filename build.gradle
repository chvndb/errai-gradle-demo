buildscript {
  ext.overridable = { prop, defaultVal ->
    hasProperty(prop) ? getProperty(prop) : defaultVal
  }

  ext.exec = { cmd ->
    cmd.execute().text.trim()
  }

  ext.versions = [ gmd: [:] ]
  versions.gwt          =   "2.8.0"
  versions.errai        =   "4.0.0.CR1"
  versions.gmd.core     =   "2.0-rc3"
  versions.gmd.jquery   =   "1.0-rc3"
  versions.gmd.table    =   "1.0-rc3"

  ext.gwt =  [ modules: [:], dir: [:], log: [:] ]
  gwt.workers           =   "2"
  gwt.modules.dev       =   "demo.errai.App"
  gwt.modules.prod      =   "demo.errai.App"
  gwt.dir.build         =   "${buildDir}/gwt"
  gwt.dir.input         =   "${gwt.dir.build}/input"
  gwt.dir.output        =   "${gwt.dir.build}/output"
  gwt.dir.extra         =   "${gwt.dir.build}/extra"
  gwt.dir.dev           =   "${gwt.dir.build}/dev"
  gwt.dir.unitcache     =   "${gwt.dir.build}/unitcache"
  gwt.log.dev           =   overridable("gwtLogDev", "INFO")
  gwt.log.prod          =   overridable("gwtLogProd", "INFO")

  ext.errai =  [ dir: [:] ]
  errai.dir.work        =   "${projectDir}/.errai"
  errai.dir.build       =   "${buildDir}/errai"
  errai.dir.output      =   "${errai.dir.build}/output"

  ext.deploy =  [ dir: [:] ]
  deploy.dir.war        =   "${projectDir}/war"
  deploy.dir.app        =   "${deploy.dir.war}/${name}.war"
}

apply plugin: "war"
apply plugin: "eclipse"

repositories {
  mavenCentral()
}

dependencies {
  providedCompile "com.google.gwt:gwt-user:$versions.gwt"
  providedCompile "com.google.gwt:gwt-dev:$versions.gwt"
  runtime "com.google.gwt:gwt-servlet:$versions.gwt"

  compile "org.jboss.errai:errai-javaee-all:$versions.errai"
  compile "org.jboss.errai:errai-jboss-as-support:$versions.errai"

  providedCompile "com.github.gwtmaterialdesign:gwt-material:$versions.gmd.core"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-addins:$versions.gmd.core"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-themes:$versions.gmd.core"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-jquery:$versions.gmd.jquery"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-table:$versions.gmd.table"
}

clean.doFirst {
  delete errai.dir.work
  delete deploy.dir.war
}

// merge GWT inputs (source, classes, resources) to ensure Errai required property files are picked
// up correctly, e.g. ErraiApp.propeties, and Errai generators are triggered
task gwtClasses(type: Copy, dependsOn: classes) {
  includeEmptyDirs false
  from sourceSets.main.output.resourcesDir
  from(sourceSets.main.java.srcDirs) {
    include "**/**.gwt.xml"
    include "**/client/**"
    include "**/shared/**"
  }
  from(sourceSets.main.output.classesDir) {
    include "**/client/**"
    include "**/shared/**"
  }
  into gwt.dir.input
}

task gwtCompile(type: JavaExec, dependsOn: gwtClasses) {
  main = "com.google.gwt.dev.Compiler"
  classpath {[ gwt.dir.input, sourceSets.main.compileClasspath ]}
  outputs.dir gwt.dir.output
  args = [
    gwt.modules.prod,
    "-war", gwt.dir.output,
    "-logLevel", gwt.log.prod,
    "-localWorkers", gwt.workers,
    "-failOnError",
    "-generateJsInteropExports",
    "-extra", gwt.dir.extra
  ].flatten()
  // avoid generated errai classes to be added to the build/classes folder
  // which in turn would trigger a redundant recompile even when nothing changes
  // NOTE: the generated classes need be added to the final WAR
  systemProperties["errai.server.classOutput"] = errai.dir.output
}

war.dependsOn gwtCompile
war {
  from gwt.dir.output                                // include GWT generated output files
  from (errai.dir.output) { into "WEB-INF/classes" } // include Errai generated output files
  rootSpec.exclude("**/client/**")                   // exclude GWT specific client-side java source code
}

task gwtDev(type: JavaExec, dependsOn: gwtCompile) {
  main = "com.google.gwt.dev.codeserver.CodeServer"
  classpath {[ gwt.dir.input, sourceSets.main.compileClasspath ]}
  doFirst { file(gwt.dir.dev).mkdirs() }
  args = [
    gwt.modules.dev,
    "-logLevel", gwt.log.dev,
    "-launcherDir", deploy.dir.app,
    "-workDir", gwt.dir.dev,
    "-style", "Pretty",
    "-generateJsInteropExports",
    "-bindAddress", "0.0.0.0"
  ].flatten()
  systemProperties["gwt.persistentunitcachedir"] = gwt.dir.unitcache
}

task redeploy(type: Copy, dependsOn: war) {
  doFirst { file(deploy.dir.app).mkdirs() }
  doFirst { delete deploy.dir.app }
  into deploy.dir.app
  with war
}

task stop {
  doLast { exec("docker-compose -f src/main/docker/wildfly.yml down") }
}

task start(dependsOn: [stop, clean]) {
  doFirst { file(deploy.dir.app).mkdirs() }
  doLast { exec("docker-compose -f src/main/docker/wildfly.yml up -d") }
  finalizedBy redeploy
}
