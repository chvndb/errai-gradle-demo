apply plugin: "war"
apply plugin: "eclipse"

ext {
  gwtDevModules = ["demo.errai.App"]
  gwtModules = gwtDevModules
  gwtLogLevel = allowCliOverride("gwtLogLevel", "INFO")
  gwtLocalWorkers = "2"
  gwtBuildDir = "$buildDir/gwt"
  gwtExtraDir = "$buildDir/gwt-extra"
  erraiBuildDir = "$buildDir/errai"
  deployDir = "$projectDir/war"
}

def allowCliOverride(prop, defaultVal) {
  hasProperty(prop) ? getProperty(prop) : defaultVal
}

def exec(cmd) {
  cmd.execute().text.trim()
}

repositories {
  mavenCentral()
}

dependencies {
  def gwt = "2.8.0"
  def errai = "4.0.0.CR1"
  def gmd = "2.0-rc3"
  def gmdJquery = "1.0-rc3"
  def gmdTable = "1.0-rc3"

  providedCompile "com.google.gwt:gwt-user:$gwt"
  providedCompile "com.google.gwt:gwt-dev:$gwt"
  runtime "com.google.gwt:gwt-servlet:$gwt"

  compile "org.jboss.errai:errai-javaee-all:$errai"
  compile "org.jboss.errai:errai-jboss-as-support:$errai"

  providedCompile "com.github.gwtmaterialdesign:gwt-material:$gmd"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-addins:$gmd"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-themes:$gmd"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-jquery:$gmdJquery"
  providedCompile "com.github.gwtmaterialdesign:gwt-material-table:$gmdTable"
}

task compileGwt(type:JavaExec) {
  outputs.dir gwtBuildDir

  main = "com.google.gwt.dev.Compiler"

  // Avoids generated errai classes to be added to the build/classes folder 
  // which in turn would trigger a redundant recompile even when nothing changes
  // NOTE: the generated classes need be added to the final WAR
  jvmArgs = ["-Derrai.server.classOutput=${erraiBuildDir}"]

  classpath {[
      sourceSets.main.java.srcDirs,
      sourceSets.main.output.resourcesDir,
      sourceSets.main.output.classesDir,
      sourceSets.main.compileClasspath
  ]}

  args = [
      gwtModules,
      "-war", gwtBuildDir,
      "-logLevel", gwtLogLevel,
      "-localWorkers", gwtLocalWorkers,
      "-failOnError",
      "-generateJsInteropExports",
      "-extra", gwtExtraDir
  ].flatten()
}
compileGwt.dependsOn compileJava
war.dependsOn compileGwt
war {
  from gwtBuildDir
  from erraiBuildDir
}

clean.doFirst {
  delete "${projectDir}/.errai"
  delete "${projectDir}/war"
}

task deploy(type: Copy) {
  doFirst { file(deployDir).mkdirs() }
  doFirst { delete "$deployDir/app.war" }
  into "$deployDir/app.war"
  with war
}
deploy.dependsOn war

task stop {
  doLast { exec("docker-compose -f src/main/docker/wildfly.yml down") }
}

task start {
  doFirst { file(deployDir).mkdirs() }
  doLast { exec("docker-compose -f src/main/docker/wildfly.yml up -d") }
}
start.dependsOn stop
start.dependsOn clean
start.finalizedBy deploy
